(* This version requires to be compiled with the standard library *)
let [Int, Int] -> Bool mod19_eq = lambda (Int m Int n) (m mod 19) == (n mod 19) end in
let [Int, Int] -> Int mod19_add = lambda (Int m Int n) (m + n) mod 19 end in
let [Int] -> Int mod19_neg = lambda (Int n) if n == 0 then 0 else (19 - (n mod 19)) end end in
let [Int, Int] -> Int mod19_times = lambda (Int m Int n) (m * n) mod 19 end in
let [Int] -> Int mod19_inv = lambda (Int a) discrete_inv 19 a end in
(* The finite field Z mod 19*)
let Int field zmod19 = field {Int, 0, mod19_eq, mod19_add, mod19_neg, 1, mod19_times, mod19_inv} in

(* The following are to set up the problem, not required in computation*)
let Int num_total = 15 in (* total number of sharers *)
let Int to_get_secret = 4 in (* number of sharers needed to obtain secret *)

(* The following are used in computation *)
let Int secret = 2 in (* f(0) in our polynomial, or y-intercept term, which holds the secret *)

(* Privately generated polynomial to generate keys *)
(* The following integers were randomly generated 0 < n < p, all polynomial coefficients must be unique *)
(* Every sharer recieves a pair (xi, yi) s.t. yi = f(xi) and they know only their pair *)
let Int poly as = zmod19.make_poly [9, 4, 18, 12, secret] in (* randomly generated coefficients *)
let Int list xs = [13, 7, 5, 14, 2] in (* unique randomly generated x value points *)

let [Int poly, Int list] -> Int list get_ys = lambda (Int poly as Int list xs) 
                                                if null? xs then [] 
                                                else (zmod19.poly_eval as (car xs))::(get_ys as (cdr xs)) end end in
let Int list ys = get_ys as xs in 
(* xs and ys are parallel lists for polynomial points, we retrieve the secret using only xs and ys *)                                                
(* using the LaGrange Polynomial Interpolation  *)

let [Int, Int list] -> Int prod = 
  lambda (Int xi Int list xjs)
    if null? xjs then 1 
    else if xi == (car xjs) then prod xi (cdr xjs)
    else zmod19.times (zmod19.times (zmod19.neg car xjs) (zmod19.inv (zmod19.plus xi (zmod19.neg car xjs)))) (prod xi (cdr xjs)) end end end in

let [Int list, Int list, Int list] -> Int sum = 
  lambda (Int list curr_xs Int list curr_ys Int list all_xs)
    if null? curr_xs then 0 
    else zmod19.plus (zmod19.times (car curr_ys) (prod (car curr_xs) all_xs)) (sum (cdr curr_xs) (cdr curr_ys) all_xs) end end

in print sum xs ys xs